apply plugin: 'maven'
apply plugin: 'signing'

// To append all the sub-projects in this one
jar {
  subprojects.each { subproject ->
    from subproject.configurations.archives.allArtifacts.files.collect {
      zipTree(it)
    }
  }
}

jar.dependsOn subprojects.tasks['assemble']

task allSourcesJar(type: Jar, dependsOn: subprojects.tasks['classes']) {
  classifier = 'sources'
  from subprojects.collect {project -> project.sourceSets.main.allSource }
}

assemble.dependsOn allSourcesJar

task allJavadoc(type: Javadoc) {
    source subprojects.collect {project -> project.sourceSets.main.allJava } 
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath}) 
    destinationDir = javadoc.destinationDir
}

javadoc.dependsOn allJavadoc

task allJavadocJar(type: Jar, dependsOn: allJavadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

assemble.dependsOn allJavadocJar

artifacts {
    archives allJavadocJar, allSourcesJar
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask(":uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      pom.project {
        name = "JL Smalltools"
        packaging 'jar'
        artifactId 'jl-smalltools'
        description 'Some libraries to do simple tasks simply'
        url 'https://github.com/foilen/java-libraries'

        scm {
          connection = "scm:git:git@github.com:foilen/java-libraries.git"
          url = "https://github.com/foilen/java-libraries"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id 'foilen'
            name 'Simon Levesque'
            email 'simon@foilen.com'
          }
        }
      }
    }
  }
}

dependencies {
}
