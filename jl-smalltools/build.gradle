plugins {
  id "com.jfrog.bintray" version "$bintrayVersion"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  
  publish = true
  
  pkg {
    repo = 'maven'
    userOrg = 'foilen'
    name = 'com.foilen:jl-smalltools'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/foilen/java-libraries.git'
  }
  
  publications = ['MyPublication'] 
}

// To append all the sub-projects in this one
jar {
  subprojects.each { subproject ->
    from subproject.configurations.archives.allArtifacts.files.collect {
      zipTree(it)
    }
  }
}

jar.dependsOn subprojects.tasks['assemble']

task allSourcesJar(type: Jar, dependsOn: subprojects.tasks['classes']) {
  classifier = 'sources'
  from subprojects.collect {project -> project.sourceSets.main.allSource }
}

assemble.dependsOn allSourcesJar

task allJavadoc(type: Javadoc) {
    source subprojects.collect {project -> project.sourceSets.main.allJava } 
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath}) 
    destinationDir = javadoc.destinationDir
}

javadoc.dependsOn allJavadoc

task allJavadocJar(type: Jar, dependsOn: allJavadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

assemble.dependsOn allJavadocJar


publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact allSourcesJar
      artifact allJavadocJar
      groupId 'com.foilen'
      artifactId 'jl-smalltools'
      
      pom {
        name = "JL Smalltools"
        description = "Some libraries to do simple tasks simply"
        url = "https://github.com/foilen/java-libraries"
        
        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }
        
        scm {
          connection = "scm:git:git@github.com:foilen/java-libraries.git"
          url = "https://github.com/foilen/java-libraries"
        }
        
      }
      
    }
  }
}

dependencies {
}

