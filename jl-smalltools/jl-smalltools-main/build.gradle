apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.foilen'
version = System.getenv('VERSION') ?: 'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 11

dependencies {
    api 'ch.qos.logback:logback-classic:1.4.1'
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
    api 'com.google.guava:guava:31.1-jre'
    api 'javax.annotation:javax.annotation-api:1.3.2'
    api 'junit:junit:4.13.2'
    api 'org.apache.commons:commons-lang3:3.12.0'
    api 'org.freemarker:freemarker:2.3.31'
    api 'org.yaml:snakeyaml:1.32'
    api 'org.slf4j:slf4j-api:2.0.2'

    testImplementation 'org.mockito:mockito-core:4.8.0'
}

jar {
    enabled = true
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv('OSSRH_USER')
                password = System.getenv('OSSRH_PASS')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "JL Smalltools"
                description = 'Some libraries to do simple tasks simply'
                url = 'https://github.com/foilen/java-libraries'
                from components.java

                scm {
                    connection = "scm:git:git@github.com:foilen/java-libraries.git"
                    url = "https://github.com/foilen/java-libraries"
                }

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id = 'foilen'
                        name = 'Simon Levesque'
                        email = 'simon@foilen.com'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
