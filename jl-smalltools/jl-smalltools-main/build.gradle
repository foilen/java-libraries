apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.foilen'
version = System.getenv('VERSION') ?: 'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 17

dependencies {
    api 'ch.qos.logback:logback-classic:1.5.3'
    api 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.0'
    api 'com.google.guava:guava:33.1.0-jre'
    api 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    api "junit:junit:$junit4Version"
    api 'org.apache.commons:commons-lang3:3.14.0'
    api 'org.freemarker:freemarker:2.3.32'
    api 'org.slf4j:slf4j-api:2.0.12'

    testImplementation 'org.mockito:mockito-core:5.11.0'
}

jar {
    enabled = true
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv('OSSRH_USER')
                password = System.getenv('OSSRH_PASS')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "JL Smalltools"
                description = 'Some libraries to do simple tasks simply'
                url = 'https://github.com/foilen/java-libraries'
                from components.java

                scm {
                    connection = "scm:git:git@github.com:foilen/java-libraries.git"
                    url = "https://github.com/foilen/java-libraries"
                }

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id = 'foilen'
                        name = 'Simon Levesque'
                        email = 'simon@foilen.com'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
