apply plugin: 'maven'
apply plugin: 'signing'

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask(":uploadArchives") }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: System.getenv('OSSRH_USER'), password: System.getenv('OSSRH_PASS'))
      }

      pom.project {
        name = "JL Smalltools"
        packaging 'jar'
        artifactId 'jl-incubator'
        description 'Some libraries to do simple tasks simply (experimental)'
        url 'https://github.com/foilen/java-libraries'

        scm {
          connection = "scm:git:git@github.com:foilen/java-libraries.git"
          url = "https://github.com/foilen/java-libraries"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id 'foilen'
            name 'Simon Levesque'
            email 'simon@foilen.com'
          }
        }
      }
    }
  }
}

dependencies {

  compileOnly project(':jl-smalltools:jl-smalltools-main')

  compileOnly 'ch.qos.logback:logback-classic:1.2.3'
  compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.9.1'
  compileOnly 'com.google.guava:guava:23.0'
  compileOnly 'com.madgag.spongycastle:prov:1.54.0.0'
  compileOnly 'com.madgag.spongycastle:pkix:1.54.0.0'
  compileOnly 'com.madgag.spongycastle:pg:1.54.0.0'
  compileOnly 'com.sun.mail:javax.mail:1.6.0'
  compileOnly 'io.netty:netty-all:5.0.0.Alpha2'
  compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
  compileOnly 'junit:junit:4.12'
  compileOnly 'org.freemarker:freemarker:2.3.23'
  compileOnly 'org.hibernate:hibernate-core:4.3.11.Final'
  compileOnly 'org.yaml:snakeyaml:1.18'
  compileOnly 'org.slf4j:slf4j-api:1.7.25'
  compileOnly 'org.springframework:spring-context-support:4.3.11.RELEASE'
  compileOnly 'org.springframework:spring-core:4.3.11.RELEASE'
  compileOnly 'org.springframework:spring-orm:4.3.11.RELEASE'
  compileOnly 'org.springframework:spring-webmvc:4.3.11.RELEASE'
  compileOnly 'org.springframework.security:spring-security-web:4.2.3.RELEASE'

  testCompile 'org.mockito:mockito-core:2.10.0'
}
