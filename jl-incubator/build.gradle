apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.foilen'
version = System.getenv('VERSION') ?: 'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 11

dependencies {

    implementation project(':jl-smalltools:jl-smalltools-main')

    implementation 'ch.qos.logback:logback-classic:1.4.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    implementation 'com.madgag.spongycastle:pkix:1.54.0.0'
    implementation 'com.madgag.spongycastle:pg:1.54.0.0'
    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'io.netty:netty-all:5.0.0.Alpha2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'junit:junit:4.13.2'
    implementation 'org.freemarker:freemarker:2.3.31'
    implementation 'org.hibernate:hibernate-core:4.3.11.Final'
    implementation 'org.yaml:snakeyaml:1.32'
    implementation 'org.slf4j:slf4j-api:2.0.2'
    implementation 'org.springframework:spring-context-support:5.3.23'
    implementation 'org.springframework:spring-core:5.3.23'
    implementation 'org.springframework:spring-orm:5.3.23'
    implementation 'org.springframework:spring-webmvc:5.3.23'
    implementation 'org.springframework.security:spring-security-web:5.7.3'

    testImplementation 'org.mockito:mockito-core:4.8.0'
}

jar {
    enabled = true
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv('OSSRH_USER')
                password = System.getenv('OSSRH_PASS')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "JL Smalltools"
                description = 'Some libraries to do simple tasks simply (experimental)'
                url = 'https://github.com/foilen/java-libraries'
                from components.java

                scm {
                    connection = "scm:git:git@github.com:foilen/java-libraries.git"
                    url = "https://github.com/foilen/java-libraries"
                }

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }

                developers {
                    developer {
                        id = 'foilen'
                        name = 'Simon Levesque'
                        email = 'simon@foilen.com'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
